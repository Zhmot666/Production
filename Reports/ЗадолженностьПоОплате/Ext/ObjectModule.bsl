
#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		СтандартнаяОбработка = Ложь;
		Если ДебиторскаяКредиторская.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Выберите тип задолженности'");
			Сообщение.Сообщить(); 
			Возврат;
		КонецЕсли;
		
		
		
		База = Неопределено;
		РезультатПодключения = Неопределено;
		ОбщегоНазначенияСервер.ПодключениеК1С77(База, РезультатПодключения);
		
		Если НЕ РезультатПодключения Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='НЕ ПОДКЛЮЧИЛИСЬ К 1С 7.7'");
			Сообщение.Сообщить(); 
			Возврат;
		КонецЕсли;
		
		Договора = Новый ТаблицаЗначений;
		Договора.Колонки.Добавить("Покупатель", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
		Договора.Колонки.Добавить("Договор", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
		Договора.Колонки.Добавить("ДатаДоговора", Новый ОписаниеТипов("Дата",,,,,Новый КвалификаторыДаты()));
		Договора.Колонки.Добавить("СуммаПросрочки", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,3)));
		Договора.Колонки.Добавить("ДнейОтсрочки", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(3,0)));
		Договора.Колонки.Добавить("ДнейПросрочки", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(3,0)));
		
		БИ = База.CreateObject("БухгалтерскиеИтоги");
		
		Если ДебиторскаяКредиторская = Перечисления.ТипЗадолженности.Дебиторская Тогда
			СчетОборотов = "62.1";
		Иначе 
			СчетОборотов = "60.1";
		КонецЕсли;
		
		СубконтоКонтрагенты = База.ВидыСубконто.Контрагенты;
		СубконтоОснование = База.ВидыСубконто.Основание;
		БИ.ИспользоватьСубконто(СубконтоКонтрагенты);
		БИ.ИспользоватьСубконто(СубконтоОснование);
		Счет10 = База.CreateObject("Счет");
		Счет10.НайтиПоКоду(СчетОборотов);
		БИ.ВыполнитьЗапрос(ТекущаяДатаСеанса(),ТекущаяДатаСеанса(),Счет10,"","","","День","С");
		БИ.ВыбратьСубконто(1);
		Пока БИ.ПолучитьСубконто(1) = 1 Цикл
			БИ.ВыбратьСубконто(2);
			Пока БИ.ПолучитьСубконто(2) = 1 Цикл
				СтрокаТЗ = Договора.Добавить();
				СтрокаТЗ.Покупатель = БИ.Субконто(1).Наименование;
				СтрокаТЗ.Договор = БИ.Субконто(2).Наименование;
				СтрокаТЗ.ДатаДоговора = БИ.Субконто(2).ДатаДоговора;
				Если ДебиторскаяКредиторская = Перечисления.ТипЗадолженности.Дебиторская Тогда
					Если БИ.СКД("С") > 0 Тогда
						СтрокаТЗ.СуммаПросрочки = БИ.СКД("С");
					Иначе
						СтрокаТЗ.СуммаПросрочки = -(БИ.СКК("С"));
					КонецЕсли;
				Иначе 
					Если БИ.СКК("С") > 0 Тогда
						СтрокаТЗ.СуммаПросрочки = БИ.СКК("С");
					Иначе 
						СтрокаТЗ.СуммаПросрочки = -(БИ.СКД("С"));
					КонецЕсли;
				КонецЕсли;
				СтрокаТЗ.ДнейОтсрочки = БИ.Субконто(2).ЧерезДней;
				СтрокаТЗ.ДнейПросрочки = Окр((ТекущаяДатаСеанса() - СтрокаТЗ.ДатаДоговора)/ОбщегоНазначенияСервер.ПолучитьКонстантуССекундами(), 0)-СтрокаТЗ.ДнейОтсрочки;
			КонецЦикла;		
		КонецЦикла;
		
		
		
		
		СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
		
		Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
		
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
		
		ВнешнийНаборДанных = Новый Структура("Договора", Договора); 
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); 
		
		ДокументРезультат.Очистить();
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
		
		
	КонецПроцедуры
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

#КонецОбласти
