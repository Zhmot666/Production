
#Область ОбработчикиСобытийЭлементовТаблицыФормыНоменклатура

&НаКлиенте
Процедура НоменклатураПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыборкаОборотовИз77(Знач База, Знач БИ, СубконтоМат, СчетОборотов, Знач ТЗОстаткиИОбороты7)
	
	Перем СтрокаОстатков, СубконтоМХ, Счет10;
	
	СубконтоМХ = База.ВидыСубконто.МестаХранения;
	СпрМХ = База.CreateObject("Справочник.МестаХранения");
	СпрМХ.НайтиПоКоду(ПараметрыСеанса.ТекущийПользователь.КодСкладаДопМатериалов);
	
	БИ.ИспользоватьСубконто(СубконтоМат);
	БИ.ИспользоватьСубконто(СубконтоМХ, СпрМХ.ТекущийЭлемент());
	Счет10 = База.CreateObject("Счет");
	Счет10.НайтиПоКоду(СчетОборотов);
	БИ.ВыполнитьЗапрос(НачалоМесяца(Объект.Дата),КонецМесяца(Объект.Дата),Счет10,"","","","Месяц","К");
	БИ.ВыбратьСубконто(1);
	Пока БИ.ПолучитьСубконто(1) = 1 Цикл
		БИ.ВыбратьСубконто(2);
		Пока БИ.ПолучитьСубконто(2) = 1 Цикл
			СтрокаОстатков = ТЗОстаткиИОбороты7.Добавить();
			СтрокаОстатков.Номенклатура = БИ.Субконто(1).Наименование;
			СтрокаОстатков.ЕдиницыИзмерения = БИ.Субконто(1).ЕдиницаИзмерения.Наименование;
			СтрокаОстатков.КодНоменклатуры = БИ.Субконто(1).Код;
			СтрокаОстатков.КонечныйОстаток =  БИ.СКД("К");
		КонецЦикла;		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	Объект.Номенклатура.Очистить();
	База = Неопределено;
    РезультатПодключения = Неопределено;
    ОбщегоНазначенияСервер.ПодключениеК1С77(База, РезультатПодключения);
	Если НЕ РезультатПодключения Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "НЕ ПОДКЛЮЧИЛИСЬ К 1С 7.7";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
	
	ТЗОстаткиИОбороты7 = Новый ТаблицаЗначений;
	ТЗОстаткиИОбороты7.Колонки.Добавить("Номенклатура");
	ТЗОстаткиИОбороты7.Колонки.Добавить("ЕдиницыИзмерения");
	ТЗОстаткиИОбороты7.Колонки.Добавить("КодНоменклатуры");
	ТЗОстаткиИОбороты7.Колонки.Добавить("КонечныйОстаток");
	
	БИ = База.CreateObject("БухгалтерскиеИтоги");
	
	СчетОборотов = "10.6";
	СубконтоМат = База.ВидыСубконто.Материалы;
	ВыборкаОборотовИз77(База, БИ, СубконтоМат, СчетОборотов, ТЗОстаткиИОбороты7);
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписаниеВспомогательныхСредствНоменклатура.Материал КАК Материал,
		|	СписаниеВспомогательныхСредствНоменклатура.Категория КАК Категория
		|ИЗ
		|	Документ.СписаниеВспомогательныхСредств.Номенклатура КАК СписаниеВспомогательныхСредствНоменклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	СписаниеВспомогательныхСредствНоменклатура.Материал,
		|	СписаниеВспомогательныхСредствНоменклатура.Категория";
	
	ТЗЗапроса = Запрос.Выполнить().Выгрузить();
	

	Для Каждого СтрокаОстатков Из ТЗОстаткиИОбороты7 Цикл
		СтрНоменклатуры = Объект.Номенклатура.Добавить();
		СтрНоменклатуры.Материал = СтрокаОстатков.Номенклатура;
		СтрНоменклатуры.ЕдиницыИзмерения = СтрокаОстатков.ЕдиницыИзмерения;
		СтрНоменклатуры.КодПоБухгалтерии = СтрокаОстатков.КодНоменклатуры;
		СтрНоменклатуры.ОстатокПоБухгалтерии = СтрокаОстатков.КонечныйОстаток;
		НайденнаяСтрока = ТЗЗапроса.Найти(СтрНоменклатуры.Материал, "Материал");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрНоменклатуры.Категория = НайденнаяСтрока.Категория;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти



